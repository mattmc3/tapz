#!/usr/bin/awk -f

# Take TAP output and remove header and footer and re-number it
# for use in a chained TAP stream

BEGIN {
  if ( ! is_digits(testnum) || testnum < 1 ) {
    testnum=1
  }
  prev_empty=0
  warn=0
  pass=0
  fail=0
}

# hold empty lines for an iteration to check if next line
# is the test plan (1..n).
/^$/ {
  if (prev_empty) {
    print ""
  }
  else {
    prev_empty=1
  }
  next
}
/^1\.{2}+[0-9]+$/           { prev_empty=0; next }
prev_empty                  { prev_empty=0; print "" }
/^ok /                      { pass++; print "ok "     testnum++ " " substr($0, index($0,$3)); next }
/^not ok .*# (TODO|SKIP)/   { pass++; print "not ok " testnum++ " " substr($0, index($0,$4)); next }
/^not ok /                  { fail++; print "not ok " testnum++ " " substr($0, index($0,$4)); next }
/^# warn(ing)?/             { warn++; print $0; next }
/^TAP version/              { next }
/^# ### ZTAP /              { next }
/^# (pass|fail) [0-9]+/     { next }
/^# ok$/                    { next }

# default action prints
{ print }

END {
  # print the state to fd3
  printf "ZTAP_PASSED=%s;ZTAP_WARNINGS=%s;ZTAP_FAILED=%s\n", pass, warn, fail | "cat 1>&3"
}

function is_digits(n) {
  return n ~ /^[0-9]+$/
}

# vi: ft=awk
